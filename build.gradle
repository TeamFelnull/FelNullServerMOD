buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}
plugins {
    id "com.github.johnrengelman.shadow" version "4.0.4"
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'

version = '2022SP-1.6'
group = 'dev.felnull'
archivesBaseName = 'FelNullServerMOD-1.16.5'

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    mappings channel: 'official', version: '1.16.5'

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            mods {
                felnullservermod {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            mods {
                felnullservermod {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            args '--mod', 'felnullservermod', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                felnullservermod {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }
repositories {
    maven {
        name = "FelNull"
        url = "https://maven.felnull.dev/"
    }
    mavenCentral()
}
configurations {
    shadowIn
    implementation.extendsFrom(shadowIn)
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.5-36.2.20'

    implementation 'dev.felnull:katyouvotifier:3.0.0-SNAPSHOT-1.1'

    shadowIn 'dev.felnull:felnull-java-library:1.41'
    shadowIn 'dev.felnull:felnull-nbs-library:1.1'
    shadowIn 'net.dv8tion:JDA:5.0.0-alpha.5'
}
ext.themanifest = {
    attributes([
            "Specification-Title"     : "katyouvotifier",
            "Specification-Vendor"    : "MORIMORI0317",
            "Specification-Version"   : "1",
            "Implementation-Title"    : project.name,
            "Implementation-Version"  : "${version}",
            "Implementation-Vendor"   : "TeamFelNull",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
    ])
}

shadowJar {
    configurations = [project.configurations.shadowIn]
    classifier ''
    relocate 'dev.felnull.fnnbs', 'dev.felnull.fnsm.libs.dev.felnull.fnnbs'
    relocate 'dev.felnull.fnjl', 'dev.felnull.fnsm.libs.dev.felnull.fnjl'
    relocate 'tomp2p.opuswrapper', 'dev.felnull.fnsm.libs.tomp2p.opuswrapper'
    relocate 'okio', 'dev.felnull.fnsm.libs.okio'
    relocate 'okhttp3', 'dev.felnull.fnsm.libs.okhttp3'
    relocate 'net.dv8tion', 'dev.felnull.fnsm.libs.net.dv8tion'
    relocate 'natives', 'dev.felnull.fnsm.libs.natives'
    relocate 'javax.annotation', 'dev.felnull.fnsm.libs.javax.annotation'
    relocate 'gnu.trove', 'dev.felnull.fnsm.libs.gnu.trove'
    relocate 'club.minnced.opus', 'dev.felnull.fnsm.libs.club.minnced.opus'
    relocate 'org.slf4j', 'dev.felnull.fnsm.libs.org.slf4j'
    relocate 'org.jetbrains', 'dev.felnull.fnsm.libs.org.jetbrains'
    relocate 'org.intellij', 'dev.felnull.fnsm.libs.org.intellij'
    relocate 'org.apache.commons.collections4', 'dev.felnull.fnsm.libs.org.apache.commons.collections4'
    relocate 'com.sun.jna', 'dev.felnull.fnsm.libs.com.sun.jna'
    relocate 'com.neovisionaries.ws', 'dev.felnull.fnsm.libs.com.neovisionaries.ws'
    relocate 'com.iwebpp.crypto', 'dev.felnull.fnsm.libs.com.iwebpp.crypto'
    relocate 'com.fasterxml.jackson', 'dev.felnull.fnsm.libs.com.fasterxml.jackson'
}

afterEvaluate {
    reobf {
        shadowJar { mappings = tasks.reobfJar.mappings }
    }
}

jar {
    manifest themanifest
}

task devJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
    manifest themanifest
}
task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
    manifest themanifest
}
artifacts {
    archives devJar
    archives sourceJar
}
jar.finalizedBy('reobfJar')